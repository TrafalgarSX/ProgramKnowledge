### Generator expressions
---
Generator expression which was introduced in CMake 3.15.

[`Generator expressions`](https://cmake.org/cmake/help/v3.25/manual/cmake-generator-expressions.7.html#manual:cmake-generator-expressions(7) "cmake-generator-expressions(7)") are evaluated during build system generation to produce information **specific to each build configuration**.

这个对于visual studio, xcode 这种multi-configuration generators很有用， 因为他们的Debug Release类型都是编译的时候确定的（`cmake --build . --cofnig Debug|Release`)，这样Generator expression 就可以 “produce  information specific to each build configuration”

[`Generator expressions`](https://cmake.org/cmake/help/v3.25/manual/cmake-generator-expressions.7.html#manual:cmake-generator-expressions(7) "cmake-generator-expressions(7)") are allowed in the context of many target properties, such as
- [`LINK_LIBRARIES`](https://cmake.org/cmake/help/v3.25/prop_tgt/LINK_LIBRARIES.html#prop_tgt:LINK_LIBRARIES "LINK_LIBRARIES"), 
- [`INCLUDE_DIRECTORIES`](https://cmake.org/cmake/help/v3.25/prop_tgt/INCLUDE_DIRECTORIES.html#prop_tgt:INCLUDE_DIRECTORIES "INCLUDE_DIRECTORIES"), 
- [`COMPILE_DEFINITIONS`](https://cmake.org/cmake/help/v3.25/prop_tgt/COMPILE_DEFINITIONS.html#prop_tgt:COMPILE_DEFINITIONS "COMPILE_DEFINITIONS")
- and others. 

They may also be used when using commands to populate those properties, such as 
- [`target_link_libraries()`](https://cmake.org/cmake/help/v3.25/command/target_link_libraries.html#command:target_link_libraries "target_link_libraries"),
- [`target_include_directories()`](https://cmake.org/cmake/help/v3.25/command/target_include_directories.html#command:target_include_directories "target_include_directories"),
- [`target_compile_definitions()`](https://cmake.org/cmake/help/v3.25/command/target_compile_definitions.html#command:target_compile_definitions "target_compile_definitions") 
- and others.(其实和上面一一对应)

Generator expressions may be used to 
- enable **conditional linking**, 
- **conditional definitions** used when compiling, 
- **conditional include directories** and more. 

💡还是和上面一一对应
The conditions may be based on the build configuration, target properties, platform information or any other queryable information.

There are different types of [`generator expressions`](https://cmake.org/cmake/help/v3.25/manual/cmake-generator-expressions.7.html#manual:cmake-generator-expressions(7) "cmake-generator-expressions(7)") including 
- Logical
- Informational
- Output expressions.

💡**Logical expressions** are used to create conditional output. The basic expressions are the 0 and 1 expressions. A $<0:...> results in the empty string, and <1:...> results in the content of .... They can also be nested.
**重中之重，要理解生成表达式的作用就先理解上面这段话**

#### Adding Compiler Warning Flags with Generator Expressions
A common usage of generator expressions is to **conditionally add compiler flags, such as those for language levels or warnings.** A nice pattern is to associate this information to an `INTERFACE` target allowing this information to propagate.

```cmake 
# TODO 4: Update the minimum required version to 3.15

cmake_minimum_required(VERSION 3.15)

# set the project name and version
project(Tutorial VERSION 1.0)

# TODO 1: Replace the following code by:
# * Creating an interface library called tutorial_compiler_flags
#   Hint: use add_library() with the INTERFACE signature
# * Add compiler feature cxx_std_11 to tutorial_compiler_flags
#   Hint: Use target_compile_features()

# specify the C++ standard
add_library(tutorial_compiler_flags INTERFACE)
target_compile_features(tutorial_compiler_flags INTERFACE cxx_std_11)

# TODO 5: Create helper variables to determine which compiler we are using:
# * Create a new variable gcc_like_cxx that is true if we are using CXX and
#   any of the following compilers: ARMClang, AppleClang, Clang, GNU, LCC
# * Create a new variable msvc_cxx that is true if we are using CXX and MSVC
# Hint: Use set() and COMPILE_LANG_AND_ID
set(msvc_cxx "$<COMPILE_LANG_AND_ID:CXX,MSVC>")
set(gcc_like_cxx "$<COMPILE_LANG_AND_ID:CXX,ARMClang,AppleClang,Clang,GNU,LCC>")

# TODO 6: Add warning flag compile options to the interface library
# tutorial_compiler_flags.
# * For gcc_like_cxx, add flags -Wall;-Wextra;-Wshadow;-Wformat=2;-Wunused
# * For msvc_cxx, add flags -W3
# Hint: Use target_compile_options()
target_compile_options(tutorial_compiler_flags INTERFACE
  "$<${gcc_like_cxx}:$<BUILD_INTERFACE:-Wall;-Wextra;-Wshadow;-Wformat=2;-Wunused>>"
  "$<${msvc_cxx}:$<BUILD_INTERFACE:-W3>>"
  )

# TODO 7: With nested generator expressions, only use the flags for the
# build-tree
# Hint: Use BUILD_INTERFACE
target_compile_options(tutorial_compiler_flags INTERFACE
  "$<${gcc_like_cxx}:$<BUILD_INTERFACE:-Wall;-Wextra;-Wshadow;-Wformat=2;-Wunused>>"
  "$<${msvc_cxx}:$<BUILD_INTERFACE:-W3>>"
)

# should we use our own math functions
option(USE_MYMATH "Use tutorial provided math implementation" ON)

# configure a header file to pass some of the CMake settings
# to the source code
configure_file(TutorialConfig.h.in TutorialConfig.h)

# add the MathFunctions library
if(USE_MYMATH)
  add_subdirectory(MathFunctions)
  list(APPEND EXTRA_LIBS MathFunctions)
endif()

# add the executable
add_executable(Tutorial tutorial.cxx)

# TODO 2: Link to tutorial_compiler_flags

target_link_libraries(Tutorial PUBLIC ${EXTRA_LIBS} tutorial_compiler_flags)

# add the binary tree to the search path for include files
# so that we will find TutorialConfig.h
target_include_directories(Tutorial PUBLIC
                           "${PROJECT_BINARY_DIR}"
                           )
```

### 概述
---
生成器表达式简单来说就是**在CMake生成构建系统的时候根据不同配置动态生成特定的内容**。比如：

1. 条件链接，如针对同一个编译目标，debug版本和release版本链接不同的库文件
2. 条件定义，如针对不同编译器，定义不同的宏

生成器表达式的格式形如`$<...>`，可以嵌套，可以用在很多构建目标的属性设置和特定的CMake命令中。值得强调的是，生成表达式被展开是在生成构建系统的时候，所以**不能通过解析配置CMakeLists.txt阶段的`message`命令打印**，文末会介绍其调试方法。


### 常用的生成器表达式
---
#### 布尔生成器表达式

##### 逻辑运算符

逻辑运算很多语言都是需要的，CMake生成器表达式中有这些：

1. `$<BOOL:string>`：如果字符串为空、`0`；不区分大小写的`FALSE`、`OFF`、`N`、`NO`、`IGNORE`、`NOTFOUND`；或者区分大小写以`-NOTFOUND`结尾的字符串，则为0，否则为1
2. `$<AND:conditions>`：逻辑与，`conditons`是以**逗号分割的条件列表**
3. `$<OR:conditions>`：逻辑或，`conditons`是以逗号分割的条件列表
4. `$<NOT:condition>`：逻辑非

> 一般来说，条件是列表的，都是使用逗号进行分割，后面不再赘述。

##### 字符串比较
1. `$<STREQUAL:string1,string2>`：判断字符串是否相等
2. `$<EQUAL:value1,value2>`：判断数值是否相等
3. `$<IN_LIST:string,list>`：判断`string`是否包含在`list`中，`list`使用**分号分割**

> 注意这里的`list`是在逗号后面的列表，所以其内容需要使用分号分割。

##### 变量查询

这个会是比较常用的，在实际使用的时候会根据不同CMake内置变量生成不同配置，核心就在于“判断”：

1. `$<TARGET_EXISTS:target>`：判断目标是否存在
2. `$<CONFIG:cfgs>`：判断编译类型配置是否包含在`cfgs`列表（比如"release,debug"）中；不区分大小写
3. `$<PLATFORM_ID:platform_ids>`：判断CMake定义的平台ID是否包含在`platform_ids`列表中
4. `$<COMPILE_LANGUAGE:languages>`：判断编译语言是否包含在`languages`列表中

#### 字符串值生成器表达式

请注意，前面都是铺垫，这里才是使用生成器表达式的**主要目的**：**生成特定的字符串**。 比如官方的例子：基于编译器ID指定include目录：

```text
include_directories(/usr/include/$<CXX_COMPILER_ID>/)
```

根据编译器的类型，`$<CXX_COMPILER_ID>`会被替换成对应的ID（比如“GNU”、“Clang”）。

##### 条件表达式

这便是本文的核心了，主要有两个格式：

1. `$<condition:true_string>`：如果条件为真，则结果为`true_string`，否则为空
2. `$<IF:condition,str1,str2>`：如果条件为真，则结果为`str1`，否则为`str2`

这里的条件一般情况下就是前面介绍的**布尔生成器表达式**。 比如要根据编译类型指定不同的编译选项，可以像下面这样：

```text
set(CMAKE_C_FLAGS_DEBUG "${CMAKE_C_FLAGS_DEBUG} -g -O0")
set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} -g -O0")
set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -O2")
set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} -O2")
```

但是使用生成器表达式可以简化成：

```text
add_compile_options("$<$<CONFIG:Debug>:-g;-O0>")
add_compile_options($<$<CONFIG:Release>:-O2>)
```

> 如果需要指定多个编译选项，必须使用双引号把生成器表达式包含起来，且选项之间使用分号。

后面这个方法适用于设置一些对所有编译器（取决于项目编译语言）都通用的编译选项，而需要设置一些编译器特有的选项时，通过设置指定编译器的编译选项（前一种方法）更加简洁明了。

> 当然，可以用表达式判断编译器ID设置不同编译选项，不过明显有些为了用而用，这是没必要的。

##### 转义字符

这比较好理解，因为有一些字符有特殊含义，所以可能需要转义，比如常用的`$<COMMA>`和`$<SEMICOLON>`，分别表示`,`和`;`。

##### 字符串操作

常用的有`$<LOWER_CASE:string>`、`$<UPPER_CASE:string>`用于转换大小写。

##### 获取变量值

获取变量的值和前文提到的变量查询很类似，前面说的变量查询是判断是否存在于指定列表中或者等于指定值。语法格式是类似的，以`CONFIG`为例：

1. 获取变量值：`$<CONFIG>`
2. 判断是否存在于列表中：`$<CONFIG:cfgs>`

##### 编译目标查询

这里的查询是指获取编译目标（通过`add_executable()`、`add_library()`命令生成的）相关的一些信息，包括：

1. `$<TARGET_FILE:tgt>`：获取编译目标的文件路径
2. `$<TARGET_FILE_NAME:tgt>`：获取编译目标的文件名
3. `$<TARGET_FILE_BASE_NAME:tgt>`：获取编译目标的基础名字，也就是文件名去掉前缀和扩展名

官网有更多详细介绍，有其他需要可以阅读：**[target-dependent-queries](https://link.zhihu.com/?target=https%3A//cmake.org/cmake/help/latest/manual/cmake-generator-expressions.7.html%23target-dependent-queries)**。

### 调试
---
调试可以通过输出到文件的方式，在`cmake`执行完之后去检查是否符合预期，比如：

```cmake
file(GENERATE OUTPUT "./generator_test.txt" CONTENT "$<$<CONFIG:Debug>:-g;-O0>,$<PLATFORM_ID>\n")
```

在MacOS中执行`cmake`，得到的结果如下：

```cmake
# cmake -B cmake-build -DCMAKE_BUILD_TYPE=Debug
...
# cat cmake-build/generator_test.txt
-g;-O0,Darwin
```

如果不想写文件，也可以添加一个自定义目标，比如：

```cmake
add_custom_target(gentest COMMAND ${CMAKE_COMMAND} -E echo "\"$<$<CONFIG:Debug>:-g;-O0>,$<PLATFORM_ID>\"")
```

> 注意这里需要双引号转义，确保生成器表达式展开之后是字符串。

在执行`cmake`之后，可以使用`make gentest`输出到生成器表达式的内容：

```text
# cd cmake-build && make gentest
-g;-O0,Darwin
Built target gentest
```